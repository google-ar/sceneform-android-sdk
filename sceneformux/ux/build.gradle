/*
 * Copyright 2018 Google LLC
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.library'

android {
    compileSdkVersion 28
    defaultConfig {
        // Sceneform requires minSdkVersion >= 24.
        minSdkVersion 24
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
    }
    compileOptions {
        // Sceneform libraries use language constructs from Java 8.
        // Add these compile options if targeting minSdkVersion < 26.
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    api "com.google.ar.sceneform:core:1.8.0"
    implementation "com.android.support:appcompat-v7:28.0.0"
    implementation "com.android.support:design:28.0.0"
}

task compileUxAssets {
}

preBuild.dependsOn compileUxAssets

apply plugin: 'com.google.ar.sceneform.plugin'

sceneform.asset('sampledata/sceneform_footprint.obj',
        'default',
        'sampledata/sceneform_footprint.sfa',
        'src/main/res/raw/sceneform_footprint')

sceneform.asset('sampledata/sceneform_face_mesh.obj',
        'sampledata/sceneform_face_mesh_material.mat',
        'sampledata/sceneform_face_mesh.sfa',
        'src/main/res/raw/sceneform_face_mesh')

sceneform.asset('sampledata/sceneform_face_mesh_occluder.obj',
        'sampledata/sceneform_face_mesh_occluder_material.mat',
        'sampledata/sceneform_face_mesh_occluder.sfa',
        'src/main/res/raw/sceneform_face_mesh_occluder')

// This ensures that sceneform assets are compiled at the right point
// of the build process, which is necessary when using sceneform
// in a library instead of an application.
sceneform.before('compileUxAssets')
